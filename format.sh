#! /bin/sh
cat "$@" |
sed '
/^[1-9][0-9]*\.[1-9][0-9]*\.[1-9][0-9]*\.[1-9][0-9]*\. [^ ]/s/^/#### /
/^[1-9][0-9]*\.[1-9][0-9]*\.[1-9][0-9]*\. [^ ]/s/^/### /
/^[1-9][0-9]*\.[1-9][0-9]*\. [^ ]/s/^/## /
/^[1-9][0-9]*\.[1-9][0-9]*\.[1-9][0-9]*\.[1-9][0-9]* [^ ]/s/^/#### /
/^[1-9][0-9]*\.[1-9][0-9]*\.[1-9][0-9]* [^ ]/s/^/### /
/^[1-9][0-9]*\.[1-9][0-9]* [^ ]/s/^/## /
/^[1-9][0-9]*\. [^ ]/s/^/# /
' |
sed -n '
/^ANSI X3\.215-1994 $/b header
/^ ANSI X3\.215-1994 $/b header
p
d
:header
N
/\n! "/s/\n[^~]*~ //
s/\n//g
s/^/\
TRAILER /
s/$/\
/
p
' |
sed '
/^<NOTOUCH>/,/^<\/NOTOUCH>/{
    /^<NOTOUCH>/{
        x
        s/\n/ /g
        s/^ //
        p
        s/.*//
        x
    }
    p
    d
}
/[.;:] *$/b flush
/^ *$/b go
$b go
/^#/b go
/^– /{
    s/^– /- /
    b go
}
/^    /b go
H
d
{
:go
# flush hold buffer
    x
    s/\n/ /g
    s/^ //
    s/ *$//
    p
# check currnt line
    x
    /^#/b nohold
    /^    /b nohold
    $b nohold
:hold
    h
    d
}
{
:nohold
    /^#/s/$/\
/
    p
    s/.*//
    x
    d

}
{
:flush
    H
    s/.*//
    x
    s/\n/ /g
    s/^ //
    b
}
' #|awk '{ print ">>", $0 }'
